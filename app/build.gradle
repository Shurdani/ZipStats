plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose'
    alias(libs.plugins.ksp)
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
    
}

android {
    namespace 'com.zipstats.app'
    compileSdk 35

    // Leer credenciales de Cloudinary desde local.properties
    def localProperties = new Properties()
    def localPropertiesFile = rootProject.file('local.properties')
    if (localPropertiesFile.exists()) {
        localPropertiesFile.withInputStream { localProperties.load(it) }
    }

    defaultConfig {
        applicationId "com.zipstats.app"
        minSdk 31
        targetSdk 34
        versionCode 49
        versionName "5.4.8"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        // Exponer credenciales en BuildConfig
        buildConfigField "String", "CLOUDINARY_CLOUD_NAME", "\"${localProperties.getProperty('cloudinary.cloud_name', 'YOUR_CLOUD_NAME')}\""
        buildConfigField "String", "CLOUDINARY_API_KEY", "\"${localProperties.getProperty('cloudinary.api_key', 'YOUR_API_KEY')}\""
        buildConfigField "String", "CLOUDINARY_API_SECRET", "\"${localProperties.getProperty('cloudinary.api_secret', 'YOUR_API_SECRET')}\""
        buildConfigField "String", "OPENWEATHER_API_KEY", "\"${localProperties.getProperty('openweather.api.key', 'YOUR_OPENWEATHER_API_KEY')}\""
        buildConfigField "String", "GOOGLE_WEATHER_API_KEY", "\"${localProperties.getProperty('google.weather.api.key', 'YOUR_GOOGLE_WEATHER_API_KEY')}\""
        buildConfigField "String", "MAPBOX_ACCESS_TOKEN", "\"${localProperties.getProperty('mapbox.access.token', 'YOUR_MAPBOX_ACCESS_TOKEN')}\""

        // Mapbox Access Token para AndroidManifest
        manifestPlaceholders = [
            mapboxAccessToken: localProperties.getProperty('mapbox.access.token', 'YOUR_MAPBOX_ACCESS_TOKEN')
        ]
    }

    signingConfigs {
        release {
            // Leer configuración de signing desde variables de entorno o local.properties
            def keystorePropertiesFile = rootProject.file("keystore.properties")
            def keystoreProperties = new Properties()

            // Intentar leer desde keystore.properties (para desarrollo local)
            if (keystorePropertiesFile.exists()) {
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            } else {
                // Usar variables de entorno (para CI/CD)
                storeFile file(System.getenv("KEYSTORE_FILE") ?: "keystore.jks")
                storePassword System.getenv("KEYSTORE_PASSWORD") ?: ""
                keyAlias System.getenv("KEY_ALIAS") ?: ""
                keyPassword System.getenv("KEY_PASSWORD") ?: ""
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    lint {
        disable 'NullSafeMutableLiveData'
        abortOnError false
        checkReleaseBuilds false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
        freeCompilerArgs += [
            "-opt-in=kotlin.RequiresOptIn",
            "-Xsuppress-version-warnings"
        ]
    }
    buildFeatures {
        compose true
        buildConfig true
    }
    packaging {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
            excludes += '/META-INF/DEPENDENCIES'
            excludes += '/META-INF/LICENSE'
            excludes += '/META-INF/LICENSE.txt'
            excludes += '/META-INF/license.txt'
            excludes += '/META-INF/NOTICE'
            excludes += '/META-INF/NOTICE.txt'
            excludes += '/META-INF/notice.txt'
            excludes += '/META-INF/ASL2.0'
        }
    }
    ndkVersion '27.1.12297006'
}

dependencies {
    // --- AndroidX Core ---
    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat
    implementation libs.androidx.activity.ktx

    // --- Material Design ---
    implementation libs.material

    // --- ConstraintLayout / CardView ---
    implementation libs.androidx.constraintlayout
    implementation libs.androidx.cardview

    // --- Lifecycle / ViewModel / Compose Runtime ---
    implementation libs.androidx.lifecycle.runtime.ktx
    implementation libs.androidx.lifecycle.viewmodel.ktx
    implementation libs.androidx.lifecycle.runtime.compose

    // --- Jetpack Compose (compatible con compileSdk 34) ---
    implementation platform(libs.androidx.compose.bom)
    implementation libs.androidx.ui
    implementation libs.androidx.ui.graphics
    implementation libs.androidx.ui.tooling.preview
    implementation libs.androidx.material3
    implementation libs.androidx.activity.compose
    implementation libs.androidx.navigation.compose
    implementation libs.androidx.navigation.runtime.ktx

    // --- Hilt (actualizado y compatible con Kotlin 2.2.20) ---
    implementation "com.google.dagger:hilt-android:${libs.versions.hilt.get()}"
    ksp "com.google.dagger:hilt-compiler:${libs.versions.hilt.get()}"
    implementation libs.androidx.hilt.navigation.compose

    // --- Mapbox Maps SDK ---
    implementation libs.mapbox.maps
    implementation libs.mapbox.compose
    implementation libs.mapbox.sdk.geojson
    implementation libs.mapbox.sdk.services
    implementation libs.mapbox.sdk.turf
    implementation libs.mapbox.sdk.core
    implementation("androidx.annotation:annotation:1.0.1")

    
    // --- Google Play Services (solo location, ya no Maps) ---
    implementation libs.play.services.base
    implementation libs.play.services.location


    // --- Firebase (BoM actualizado) ---
    implementation platform(libs.firebase.bom)
    implementation 'com.google.firebase:firebase-auth-ktx'
    implementation 'com.google.firebase:firebase-firestore-ktx'
    implementation 'com.google.firebase:firebase-storage-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'

    // --- Google Sign In ---
    implementation libs.play.services.auth
    // --- Material Icons ---
    implementation 'androidx.compose.material:material-icons-core'
    implementation 'androidx.compose.material:material-icons-extended'

    // --- Coil (imágenes en Compose) ---
    implementation libs.coil.compose

    // --- Retrofit (para APIs externas, OpenWeather, etc.) ---
    implementation libs.retrofit
    implementation libs.retrofit.converter.gson
    implementation libs.kotlinx.coroutines.android

    // --- Excel (Apache POI - versión actualizada y optimizada) ---
    implementation libs.poi
    // Excluir módulos innecesarios para reducir tamaño del APK
    implementation(libs.poi.ooxml) {
        exclude group: 'org.apache.poi', module: 'poi-scratchpad'
        exclude group: 'org.apache.xmlgraphics', module: 'batik-svg'
        exclude group: 'org.apache.xmlgraphics', module: 'batik-util'
        exclude group: 'org.apache.xmlgraphics', module: 'batik-awt-util'
    }

    // --- DataStore Preferences ---
    implementation libs.datastore.preferences

    // --- HBRecorder (Screen Recording) ---
    implementation("com.github.HBiSoft:HBRecorder:3.0.9")

    // --- Testing ---
    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core
    androidTestImplementation libs.androidx.ui.test.junit4.version
    debugImplementation libs.androidx.ui.tooling.version
    debugImplementation libs.androidx.ui.test.manifest.version
}

ksp {
    arg("dagger.hilt.shareTestComponents", "true")
}

hilt {
    enableAggregatingTask = true
}

// Configuración de memoria para R8
android.applicationVariants.all { variant ->
    if (variant.buildType.name == "release") {
        variant.mergeAssetsProvider.configure {
            doLast {
                // Configurar memoria para R8
                System.setProperty("org.gradle.jvmargs", "-Xmx4g -XX:MaxMetaspaceSize=1g")
            }
        }
    }
}

