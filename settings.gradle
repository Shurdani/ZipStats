pluginManagement {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
        maven { url "https://maven.google.com" }
        maven {url = uri("https://api.mapbox.com/downloads/v2/releases/maven")}
    }
}

// Leer local.properties antes del bloque dependencyResolutionManagement
def localProperties = new Properties()
def localPropertiesFile = file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withInputStream { localProperties.load(it) }
}

dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.PREFER_SETTINGS)
    repositories {
        google()
        mavenCentral()
        maven { url "https://maven.google.com" }
        maven { url "https://jitpack.io" } // Para HBRecorder
        maven {
            url = uri("https://api.mapbox.com/downloads/v2/releases/maven")
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                // Do not change the username below.
                // This should always be `mapbox` (not your username).
                username = "mapbox"
                // Use the secret token from gradle.properties or local.properties
                def downloadsToken = providers.gradleProperty("MAPBOX_DOWNLOADS_TOKEN").orNull
                if (downloadsToken == null) {
                    // Intentar leer desde local.properties como fallback
                    downloadsToken = localProperties.getProperty('MAPBOX_DOWNLOADS_TOKEN', '')
                }
                password = downloadsToken ?: ''
            }
        }
    }
}

rootProject.name = "ZipStats"
include ':app' 